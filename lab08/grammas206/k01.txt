# $k01
    $id    $idq    $dec    $int 
  $bool    $str       (       ) 
      +       -       *       / 
      <       =       >      <= 
     >=     and     not      or 
   cond    else      if     let 
 define display newline    set! 

#
      S -> PROG #1
   PROG -> CALCS #2 |
           DEFS #3 |
           DEFS CALCS #4
      E -> $id #5 |
           $int #6 |
           $dec #7 |
           ADD #8 |
           SUB #9 |
           DIV #10 |
           MUL #11 |
           IF #12 |
           EASYLET #13 |
           CPROC #14
    ADD -> HADD E ) #15
   HADD -> ( + #16 |
           HADD E #17
    SUB -> ( - E ) #18
    DIV -> ( / E E ) #19
    MUL -> HMUL E ) #20
   HMUL -> ( * #21 |
           HMUL E #22
     IF -> ( if BOOL E E ) #23
EASYLET -> HEASYL E ) #24
 HEASYL -> ( let ( ) #25 |
           HEASYL INTER #26
    STR -> $str #27 |
           SIF #28
    SIF -> ( if BOOL STR STR ) #29
  CPROC -> HCPROC ) #30
 HCPROC -> ( $id #31 |
           HCPROC E #32
   BOOL -> $bool #33 |
           $idq #34 |
           REL #35 |
           OR #36 |
           AND #37 |
           CPRED #38
  CPRED -> HCPRED ) #39
 HCPRED -> ( $idq #40 |
           HCPRED ARG #41
    ARG -> E #42 |
           BOOL #43
    REL -> ( = E E ) #44 |
           ( < E E ) #45
     OR -> HOR BOOL ) #46
    HOR -> ( or #47 |
           HOR BOOL #48
    AND -> HAND BOOL ) #49
   HAND -> ( and #50 |
           HAND BOOL #51
    SET -> HSET E ) #52
   HSET -> ( set! $id #53
DISPSET -> ( display E ) #54 |
           ( display BOOL ) #55 |
           ( display STR ) #56 |
           ( newline ) #57 |
           SET #58
  INTER -> DISPSET #59 |
           E #60
  CALCS -> CALC #61 |
           CALCS CALC #62
   CALC -> E #63 |
           BOOL #64 |
           STR #65 |
           DISPSET #66
   DEFS -> DEF #67 |
           DEFS DEF #68
    DEF -> PRED #69 |
           VAR #70 |
           PROC #71
   PRED -> HPRED BOOL ) #72
  HPRED -> PDPAR ) #73
  PDPAR -> ( define ( $idq #74 |
           PDPAR $idq #75 |
           PDPAR $id #76
    VAR -> VARDCL E ) #77
 VARDCL -> ( define $id #78
   PROC -> HPROC BLOCK ) #79 |
           HPROC E ) #80
  HPROC -> PCPAR ) #81 |
           HPROC INTER #82
  PCPAR -> ( define ( $id #83 |
           PCPAR $id #84
  BLOCK -> HBLOCK E ) #85
 HBLOCK -> BLVAR ) #86 |
           HBLOCK INTER #87
  BLVAR -> ( let ( LOCDEF #88 |
           BLVAR LOCDEF #89
 LOCDEF -> ( $id E ) #90

