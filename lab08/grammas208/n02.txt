# $n02
    $id    $idq    $dec    $int 
  $bool    $str       (       ) 
      +       -       *       / 
      <       =       >      <= 
     >=     and     not      or 
   cond    else      if     let 
 define display newline    set! 

#
      S -> PROG #1
   PROG -> CALCS #2 |
           DEFS #3 |
           DEFS CALCS #4
      E -> $id #5 |
           $int #6 |
           $dec #7 |
           ADD #8 |
           SUB #9 |
           DIV #10 |
           MUL #11 |
           COND #12 |
           CPROC #13
    ADD -> HADD E ) #14
   HADD -> ( + #15 |
           HADD E #16
    SUB -> ( - E E ) #17
    DIV -> ( / E ) #18
    MUL -> HMUL E ) #19
   HMUL -> ( * #20 |
           HMUL E #21
   COND -> HCOND CLAUS ) #22
  HCOND -> ( cond #23 |
           HCOND CLAUS #24
  CLAUS -> HCLAUS E ) #25
 HCLAUS -> ( BOOL #26 |
           HCLAUS INTER #27
    STR -> $str #28
  CPROC -> HCPROC ) #29
 HCPROC -> ( $id #30 |
           HCPROC E #31
   BOOL -> $bool #32 |
           $idq #33 |
           REL #34 |
           OR #35 |
           AND #36 |
           CPRED #37
  CPRED -> HCPRED ) #38
 HCPRED -> ( $idq #39 |
           HCPRED ARG #40
    ARG -> E #41 |
           BOOL #42
    REL -> ( = E E ) #43 |
           ( > E E ) #44
     OR -> HOR BOOL ) #45
    HOR -> ( or #46 |
           HOR BOOL #47
    AND -> HAND BOOL ) #48
   HAND -> ( and #49 |
           HAND BOOL #50
    SET -> HSET E ) #51
   HSET -> ( set! $id #52
DISPSET -> ( display E ) #53 |
           ( display BOOL ) #54 |
           ( display STR ) #55 |
           ( newline ) #56 |
           SET #57
  INTER -> DISPSET #58 |
           E #59
  CALCS -> CALC #60 |
           CALCS CALC #61
   CALC -> E #62 |
           BOOL #63 |
           STR #64 |
           DISPSET #65
   DEFS -> DEF #66 |
           DEFS DEF #67
    DEF -> PRED #68 |
           VAR #69 |
           PROC #70
   PRED -> HPRED BOOL ) #71
  HPRED -> PDPAR ) #72
  PDPAR -> ( define ( $idq #73 |
           PDPAR $idq #74 |
           PDPAR $id #75
    VAR -> VARDCL E ) #76
 VARDCL -> ( define $id #77
   PROC -> HPROC BLOCK ) #78 |
           HPROC E ) #79
  HPROC -> PCPAR ) #80 |
           HPROC INTER #81
  PCPAR -> ( define ( $id #82 |
           PCPAR $id #83
  BLOCK -> HBLOCK E ) #84
 HBLOCK -> BLVAR ) #85 |
           HBLOCK INTER #86
  BLVAR -> ( let ( LOCDEF #87 |
           BLVAR LOCDEF #88
 LOCDEF -> ( $id E ) #89

