# $n01
    $id    $idq    $dec    $int 
  $bool    $str       (       ) 
      +       -       *       / 
      <       =       >      <= 
     >=     and     not      or 
   cond    else      if     let 
 define display newline    set! 

#
      S -> PROG #1
   PROG -> CALCS #2 |
           DEFS #3 |
           DEFS CALCS #4
      E -> $id #5 |
           $int #6 |
           $dec #7 |
           ADD #8 |
           SUB #9 |
           DIV #10 |
           MUL #11 |
           COND #12 |
           CPROC #13
    ADD -> HADD E ) #14
   HADD -> ( + #15 |
           HADD E #16
    SUB -> HSUB E ) #17
   HSUB -> ( - #18 |
           HSUB E #19
    DIV -> ( / E ) #20
    MUL -> HMUL E ) #21
   HMUL -> ( * #22 |
           HMUL E #23
   COND -> HCOND ELSE ) #24
  HCOND -> ( cond #25 |
           HCOND CLAUS #26
  CLAUS -> HCLAUS E ) #27
 HCLAUS -> ( BOOL #28 |
           HCLAUS INTER #29
   ELSE -> HELSE E ) #30
  HELSE -> ( else #31 |
           HELSE INTER #32
    STR -> $str #33
  CPROC -> HCPROC ) #34
 HCPROC -> ( $id #35 |
           HCPROC E #36
   BOOL -> $bool #37 |
           $idq #38 |
           REL #39 |
           CPRED #40
  CPRED -> HCPRED ) #41
 HCPRED -> ( $idq #42 |
           HCPRED ARG #43
    ARG -> E #44 |
           BOOL #45
    REL -> ( = E E ) #46 |
           ( < E E ) #47
    SET -> HSET E ) #48
   HSET -> ( set! $id #49
DISPSET -> ( display E ) #50 |
           ( display BOOL ) #51 |
           ( display STR ) #52 |
           ( newline ) #53 |
           SET #54
  INTER -> DISPSET #55 |
           E #56
  CALCS -> CALC #57 |
           CALCS CALC #58
   CALC -> E #59 |
           BOOL #60 |
           STR #61 |
           DISPSET #62
   DEFS -> DEF #63 |
           DEFS DEF #64
    DEF -> PRED #65 |
           VAR #66 |
           PROC #67
   PRED -> HPRED BOOL ) #68
  HPRED -> PDPAR ) #69
  PDPAR -> ( define ( $idq #70 |
           PDPAR $idq #71 |
           PDPAR $id #72
    VAR -> VARDCL E ) #73
 VARDCL -> ( define $id #74
   PROC -> HPROC BLOCK ) #75 |
           HPROC E ) #76
  HPROC -> PCPAR ) #77 |
           HPROC INTER #78
  PCPAR -> ( define ( $id #79 |
           PCPAR $id #80
  BLOCK -> HBLOCK E ) #81
 HBLOCK -> BLVAR ) #82 |
           HBLOCK INTER #83
  BLVAR -> ( let ( LOCDEF #84 |
           BLVAR LOCDEF #85
 LOCDEF -> ( $id E ) #86

