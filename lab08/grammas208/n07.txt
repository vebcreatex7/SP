# $n07
    $id    $idq    $dec    $int 
  $bool    $str       (       ) 
      +       -       *       / 
      <       =       >      <= 
     >=     and     not      or 
   cond    else      if     let 
 define display newline    set! 

#
      S -> PROG #1
   PROG -> CALCS #2 |
           DEFS #3 |
           DEFS CALCS #4
      E -> $id #5 |
           $int #6 |
           $dec #7 |
           ADD #8 |
           SUB #9 |
           DIV #10 |
           MUL #11 |
           COND #12 |
           EASYLET #13 |
           CPROC #14
    ADD -> HADD E ) #15
   HADD -> ( + #16 |
           HADD E #17
    SUB -> HSUB E ) #18
   HSUB -> ( - #19 |
           HSUB E #20
    DIV -> HDIV E ) #21
   HDIV -> ( / #22 |
           HDIV E #23
    MUL -> HMUL E ) #24
   HMUL -> ( * #25 |
           HMUL E #26
   COND -> HCOND ELSE ) #27
  HCOND -> ( cond #28 |
           HCOND CLAUS #29
  CLAUS -> ( BOOL E ) #30
   ELSE -> ( else E ) #31
EASYLET -> HEASYL E ) #32
 HEASYL -> ( let ( ) #33 |
           HEASYL INTER #34
    STR -> $str #35 |
           SIF #36
    SIF -> ( if BOOL STR STR ) #37
  CPROC -> HCPROC ) #38
 HCPROC -> ( $id #39 |
           HCPROC E #40
   BOOL -> $bool #41 |
           $idq #42 |
           REL #43 |
           ( not BOOL ) #44 |
           CPRED #45
  CPRED -> HCPRED ) #46
 HCPRED -> ( $idq #47 |
           HCPRED ARG #48
    ARG -> E #49 |
           BOOL #50
    REL -> ( = E E ) #51 |
           ( <= E E ) #52
    SET -> HSET E ) #53
   HSET -> ( set! $id #54
DISPSET -> ( display E ) #55 |
           ( display BOOL ) #56 |
           ( display STR ) #57 |
           ( newline ) #58 |
           SET #59
  INTER -> DISPSET #60 |
           E #61
  CALCS -> CALC #62 |
           CALCS CALC #63
   CALC -> E #64 |
           BOOL #65 |
           STR #66 |
           DISPSET #67
   DEFS -> DEF #68 |
           DEFS DEF #69
    DEF -> PRED #70 |
           VAR #71 |
           PROC #72
   PRED -> HPRED BOOL ) #73
  HPRED -> PDPAR ) #74
  PDPAR -> ( define ( $idq #75 |
           PDPAR $idq #76 |
           PDPAR $id #77
    VAR -> VARDCL E ) #78
 VARDCL -> ( define $id #79
   PROC -> HPROC BLOCK ) #80 |
           HPROC E ) #81
  HPROC -> PCPAR ) #82 |
           HPROC INTER #83
  PCPAR -> ( define ( $id #84 |
           PCPAR $id #85
  BLOCK -> HBLOCK E ) #86
 HBLOCK -> BLVAR ) #87 |
           HBLOCK INTER #88
  BLVAR -> ( let ( LOCDEF #89 |
           BLVAR LOCDEF #90
 LOCDEF -> ( $id E ) #91

